#include <common.h>
#include <pscommon.h>

struct ListNode
{
	uint packedColor;
	float depth;
	uint next;
};
globallycoherent RWTexture2D<uint> headBuffer;
globallycoherent RWStructuredBuffer<ListNode> fragmentsList;

uint packColor(float4 color)
{
	return (uint(color.r * 255) << 24) | (uint(color.g * 255) << 16) | (uint(color.b * 255) << 8) | uint(color.a * 255);
}

[earlydepthstencil]
float4 main(VS_OUTPUT input) : SV_TARGET
{
	float3 color = computeColor(input);
	uint newHeadBufferValue = fragmentsList.IncrementCounter();
	if (newHeadBufferValue == 0xffffffff) { return float4(0, 0, 0, 0); }
	
	uint2 upos = uint2(input.position.xy);
	uint previosHeadBufferValue;
	InterlockedExchange(headBuffer[upos], newHeadBufferValue, previosHeadBufferValue);
	
	float currentDepth = input.uv0_depth.z;
	ListNode node;
	node.packedColor = packColor(float4(color, 0.3));
	node.depth = currentDepth;
	node.next = previosHeadBufferValue;
	fragmentsList[newHeadBufferValue] = node;
	
	return float4(0, 0, 0, 0);
}